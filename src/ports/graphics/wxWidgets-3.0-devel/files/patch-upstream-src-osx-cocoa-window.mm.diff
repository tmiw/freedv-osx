http://trac.wxwidgets.org/ticket/15345
http://trac.wxwidgets.org/changeset/74613
http://trac.wxwidgets.org/changeset/74945

--- include/wx/osx/cocoa/private.h.orig
+++ include/wx/osx/cocoa/private.h
@@ -161,6 +161,7 @@ public :
     virtual void                cursorUpdate(WX_NSEvent event, WXWidget slf, void* _cmd);
     virtual void                keyEvent(WX_NSEvent event, WXWidget slf, void* _cmd);
     virtual void                insertText(NSString* text, WXWidget slf, void* _cmd);
+    virtual void                doCommandBySelector(void* sel, WXWidget slf, void* _cmd);
     virtual bool                performKeyEquivalent(WX_NSEvent event, WXWidget slf, void* _cmd);
     virtual bool                acceptsFirstResponder(WXWidget slf, void* _cmd);
     virtual bool                becomeFirstResponder(WXWidget slf, void* _cmd);
--- src/osx/cocoa/window.mm.orig
+++ src/osx/cocoa/window.mm
@@ -110,6 +110,22 @@ NSRect wxOSXGetFrameForControl( wxWindowMac* window , const wxPoint& pos , const
 
 @end // wxNSView
 
+@interface wxNSView(TextInput) <NSTextInputClient>
+
+- (void)insertText:(id)aString replacementRange:(NSRange)replacementRange;
+- (void)doCommandBySelector:(SEL)aSelector;
+- (void)setMarkedText:(id)aString selectedRange:(NSRange)selectedRange replacementRange:(NSRange)replacementRange;
+- (void)unmarkText;
+- (NSRange)selectedRange;
+- (NSRange)markedRange;
+- (BOOL)hasMarkedText;
+- (NSAttributedString *)attributedSubstringForProposedRange:(NSRange)aRange actualRange:(NSRangePointer)actualRange;
+- (NSArray*)validAttributesForMarkedText;
+- (NSRect)firstRectForCharacterRange:(NSRange)aRange actualRange:(NSRangePointer)actualRange;
+- (NSUInteger)characterIndexForPoint:(NSPoint)aPoint;
+
+@end
+
 @interface NSView(PossibleMethods)
 - (void)setTitle:(NSString *)aString;
 - (void)setStringValue:(NSString *)aString;
@@ -837,6 +853,73 @@ void wxWidgetCocoaImpl::SetupMouseEvent( wxMouseEvent &wxevent , NSEvent * nsEve
 
 @end // wxNSView
 
+// We need to adopt NSTextInputClient protocol in order to interpretKeyEvents: to work.
+// Currently, only insertText:(replacementRange:) is
+// implemented here, and the rest of the methods are stubs.
+// It is hoped that someday IME-related functionality is implemented in
+// wxWidgets and the methods of this protocol are fully working.
+
+@implementation wxNSView(TextInput)
+
+void wxOSX_insertText(NSView* self, SEL _cmd, NSString* text);
+
+- (void)insertText:(id)aString replacementRange:(NSRange)replacementRange
+{
+    wxOSX_insertText(self, @selector(insertText:), aString);
+}
+
+- (void)doCommandBySelector:(SEL)aSelector
+{
+    wxWidgetCocoaImpl* impl = (wxWidgetCocoaImpl* ) wxWidgetImpl::FindFromWXWidget( self );
+    if (impl)
+        impl->doCommandBySelector(aSelector, self, _cmd);
+}
+
+- (void)setMarkedText:(id)aString selectedRange:(NSRange)selectedRange replacementRange:(NSRange)replacementRange
+{
+}
+
+- (void)unmarkText
+{
+}
+
+- (NSRange)selectedRange
+{    
+    return NSMakeRange(NSNotFound, 0);
+}
+
+- (NSRange)markedRange
+{
+    return NSMakeRange(NSNotFound, 0);
+}
+
+- (BOOL)hasMarkedText
+{
+    return NO;
+}
+
+- (NSAttributedString *)attributedSubstringForProposedRange:(NSRange)aRange actualRange:(NSRangePointer)actualRange
+{
+    return nil;
+}
+
+- (NSArray*)validAttributesForMarkedText
+{
+    return nil;
+}
+
+- (NSRect)firstRectForCharacterRange:(NSRange)aRange actualRange:(NSRangePointer)actualRange
+{
+    return NSMakeRect(0, 0, 0, 0);
+}
+- (NSUInteger)characterIndexForPoint:(NSPoint)aPoint
+{
+    return NSNotFound;
+}
+
+@end // wxNSView(TextInput)
+
+
 //
 // event handlers
 //
@@ -1286,13 +1369,80 @@ void wxWidgetCocoaImpl::keyEvent(WX_NSEvent event, WXWidget slf, void *_cmd)
 
 void wxWidgetCocoaImpl::insertText(NSString* text, WXWidget slf, void *_cmd)
 {
-    if ( m_lastKeyDownEvent==NULL || m_hasEditor || !DoHandleCharEvent(m_lastKeyDownEvent, text) )
+    bool result = false;
+    if ( IsUserPane() && !m_hasEditor && [text length] > 0)
+    {
+        if ( m_lastKeyDownEvent!=NULL && [text isEqualToString:[m_lastKeyDownEvent characters]])
+        {
+            // If we have a corresponding key event, send wxEVT_KEY_DOWN now.
+            // (see also: wxWidgetCocoaImpl::DoHandleKeyEvent)
+            {
+                wxKeyEvent wxevent(wxEVT_KEY_DOWN);
+                SetupKeyEvent( wxevent, m_lastKeyDownEvent );
+                result = GetWXPeer()->OSXHandleKeyEvent(wxevent);
+            }
+
+            // ...and wxEVT_CHAR.
+            result = result || DoHandleCharEvent(m_lastKeyDownEvent, text);
+        }
+        else
+        {
+            // If we don't have a corresponding key event (e.g. IME-composed
+            // characters), send wxEVT_CHAR without sending wxEVT_KEY_DOWN.
+            for (NSUInteger i = 0; i < [text length]; ++i)
+            {
+                wxKeyEvent wxevent(wxEVT_CHAR);
+                wxevent.m_shiftDown = wxevent.m_controlDown = wxevent.m_altDown = wxevent.m_metaDown = false;
+                wxevent.m_rawCode = 0;
+                wxevent.m_rawFlags = 0;
+                wxevent.SetTimestamp();
+                unichar aunichar = [text characterAtIndex:i];
+                wxevent.m_uniChar = aunichar;
+                wxevent.m_keyCode = aunichar < 0x80 ? aunichar : WXK_NONE;
+                wxWindowMac* peer = GetWXPeer();
+                if ( peer )
+                {
+                    wxevent.SetEventObject(peer);
+                    wxevent.SetId(peer->GetId());
+                }
+                result = GetWXPeer()->OSXHandleKeyEvent(wxevent) || result;
+            }
+        }
+    }
+    if ( !result )
     {
         wxOSX_TextEventHandlerPtr superimpl = (wxOSX_TextEventHandlerPtr) [[slf superclass] instanceMethodForSelector:(SEL)_cmd];
         superimpl(slf, (SEL)_cmd, text);
     }
 }
 
+void wxWidgetCocoaImpl::doCommandBySelector(void* sel, WXWidget slf, void* _cmd)
+{
+    if ( m_lastKeyDownEvent!=NULL )
+    {
+        // If we have a corresponding key event, send wxEVT_KEY_DOWN now.
+        // (see also: wxWidgetCocoaImpl::DoHandleKeyEvent)
+        wxKeyEvent wxevent(wxEVT_KEY_DOWN);
+        SetupKeyEvent( wxevent, m_lastKeyDownEvent );
+        bool result = GetWXPeer()->OSXHandleKeyEvent(wxevent);
+
+        if (!result)
+        {
+            // Generate wxEVT_CHAR if wxEVT_KEY_DOWN is not handled.
+
+            long keycode = wxOSXTranslateCocoaKey( m_lastKeyDownEvent, wxEVT_CHAR );
+
+            wxKeyEvent wxevent2(wxevent) ;
+            wxevent2.SetEventType(wxEVT_CHAR);
+            SetupKeyEvent( wxevent2, m_lastKeyDownEvent );
+            if ( (keycode > 0 && keycode < WXK_SPACE) || keycode == WXK_DELETE || keycode >= WXK_START )
+            {
+                wxevent2.m_keyCode = keycode;
+            }
+            GetWXPeer()->OSXHandleKeyEvent(wxevent2);
+        }
+    }
+}
 
 bool wxWidgetCocoaImpl::performKeyEquivalent(WX_NSEvent event, WXWidget slf, void *_cmd)
 {
@@ -2555,10 +2705,18 @@ void wxWidgetCocoaImpl::InstallEventHandler( WXWidget control )
 
 bool wxWidgetCocoaImpl::DoHandleCharEvent(NSEvent *event, NSString *text)
 {
-    wxKeyEvent wxevent(wxEVT_CHAR);
-    SetupKeyEvent( wxevent, event, text );
+    bool result = false;
+    
+    for (NSUInteger i = 0; i < [text length]; ++i)
+    {
+        wxKeyEvent wxevent(wxEVT_CHAR);
+        unichar c = [text characterAtIndex:i];
+        SetupKeyEvent( wxevent, event, [NSString stringWithCharacters:&c length:1]);
 
-    return GetWXPeer()->OSXHandleKeyEvent(wxevent);
+        result = GetWXPeer()->OSXHandleKeyEvent(wxevent) || result;
+    }
+    
+    return result;
 }
 
 bool wxWidgetCocoaImpl::DoHandleKeyEvent(NSEvent *event)
@@ -2577,48 +2735,23 @@ bool wxWidgetCocoaImpl::DoHandleKeyEvent(NSEvent *event)
             return true;
     }
 
-    bool result = GetWXPeer()->OSXHandleKeyEvent(wxevent);
-
-    // this will fire higher level events, like insertText, to help
-    // us handle EVT_CHAR, etc.
+    if ( IsUserPane() && [event type] == NSKeyDown)
+    {
+        // Don't fire wxEVT_KEY_DOWN here in order to allow IME to intercept
+        // some key events. If the event is not handled by IME, either
+        // insertText: or doCommandBySelector: is called, so we send
+        // wxEVT_KEY_DOWN and wxEVT_CHAR there.
 
-    if ( !result )
+        if ( [m_osxView isKindOfClass:[NSScrollView class] ] )
+            [[(NSScrollView*)m_osxView documentView] interpretKeyEvents:[NSArray arrayWithObject:event]];
+        else
+            [m_osxView interpretKeyEvents:[NSArray arrayWithObject:event]];
+        return true;
+    }
+    else
     {
-        if ( [event type] == NSKeyDown)
-        {
-            long keycode = wxOSXTranslateCocoaKey( event, wxEVT_CHAR );
-            
-            if ( (keycode > 0 && keycode < WXK_SPACE) || keycode == WXK_DELETE || keycode >= WXK_START )
-            {
-                // eventually we could setup a doCommandBySelector catcher and retransform this into the wx key chars
-                wxKeyEvent wxevent2(wxevent) ;
-                wxevent2.SetEventType(wxEVT_CHAR);
-                SetupKeyEvent( wxevent2, event );
-                wxevent2.m_keyCode = keycode;
-                result = GetWXPeer()->OSXHandleKeyEvent(wxevent2);
-            }
-            else if (wxevent.CmdDown())
-            {
-                wxKeyEvent wxevent2(wxevent) ;
-                wxevent2.SetEventType(wxEVT_CHAR);
-                SetupKeyEvent( wxevent2, event );
-                result = GetWXPeer()->OSXHandleKeyEvent(wxevent2);
-            }
-            else
-            {
-                if ( IsUserPane() && !wxevent.CmdDown() )
-                {
-                    if ( [m_osxView isKindOfClass:[NSScrollView class] ] )
-                        [[(NSScrollView*)m_osxView documentView] interpretKeyEvents:[NSArray arrayWithObject:event]];
-                    else
-                        [m_osxView interpretKeyEvents:[NSArray arrayWithObject:event]];
-                    result = true;
-                }
-            }
-        }
+        return GetWXPeer()->OSXHandleKeyEvent(wxevent);
     }
-
-    return result;
 }
 
 bool wxWidgetCocoaImpl::DoHandleMouseEvent(NSEvent *event)
